---
layout: ""
page_title: "Provider: IBM Turbonomic"
description: |-
  The Turbonomic provider supplies data resources to interact with the Turbonomic API.
---

# IBM Turbonomic provider

The Turbonomic provider supplies data resources to interact with the Turbonomic API.

The provider sets Turbonomic as the source of truth for scaling decisions about cloud
entities that are deployed through Terraform. The data source of the provider returns
the tier size based on Turbonomic scaling action recommendations, or the current size
if no scaling action exists.

## Examples

The following examples demonstrate the Terraform code to and use the provider in different cloud environments.

### Configuring the provider credentials

Before you can use the provider, you must configure it with credentials.  You can use either username and password or OAuth 2.0.

#### Example using username/password credentials

```terraform
terraform {
  required_providers {
    turbonomic = {
      source  = "IBM/turbonomic"
      version = "1.2.0"
    }
  }
}

provider "turbonomic" {
  hostname   = var.hostname
  username   = var.username
  password   = var.password
  skipverify = var.skipverify
}
```

#### Example using OAuth 2.0 credentials

In order to authenticate to Turbonomic's API using OAuth 2.0, you first need to create an OAuth client.
For more information, see [Creating and authenticating an OAuth 2.0 client](https://www.ibm.com/docs/en/tarm/8.15.0?topic=cookbook-authenticating-oauth-20-clients-api#cookbook_administration_oauth_authentication__title__4)
to create the client.  Note, you must create the OAuth 2.0 client using `client_secret_basic` as the `clientAuthenticationMethods`. The output from the preceeding documentation will result in the following parameters:
- clientId
- clientSecret
- role

```terraform
terraform {
  required_providers {
    turbonomic = {
      source  = "IBM/turbonomic"
      version = "1.2.0"
    }
  }
}

provider "turbonomic" {
  hostname      = var.hostname
  client_id     = var.client_id
  client_secret = var.client_secret
  role          = var.role
  skipverify    = var.skipverify
}
```

**Note:** Valid roles are ADMINISTRATOR, SITE_ADMIN, AUTOMATOR, DEPLOYER, ADVISOR, OBSERVER, OPERATIONAL_OBSERVER, SHARED_ADVISOR, SHARED_OBSERVER and REPORT_EDITOR.

<!-- schema generated by tfplugindocs -->
## Schema

### Optional

- `client_id` (String) the OAuth 2.0 client ID that can be used to access the Turbonomic instance; use TURBO_CLIENT_ID to set with an enviornment variable
- `client_secret` (String, Sensitive) the OAuth 2.0 client secret that can be used to access the Turbonomic instance; use TURBO_CLIENT_SECRET to set with an enviornment variable
- `hostname` (String) hostname or IP Address of Turbonomic Instance; use TURBO_HOSTNAME to set with an enviornment variable
- `password` (String, Sensitive) password for the username to access the Turbonomic Instance; use TURBO_PASSWORD to set with an enviornment variable
- `role` (String) the OAuth 2.0 role that can be used to access the Turbonomic instance; use TURBO_ROLE to set with an enviornment variable
- `skipverify` (Boolean) boolean on whether to verify the SSL or TLS certificate for the hostname
- `username` (String) username to access the Turbonomic Instance; use TURBO_USERNAME to set with an enviornment variable

### Example data source configuration

The following example shows the Turbonomic data source struct that you can use in different cloud environments.

```terraform
data "turbonomic_cloud_entity_recommendation" "example" {
  entity_name  = "exampleVirtualMachine"
  entity_type  = "VirtualMachine"
  default_size = "defaultSize"
}
```

### Tagging the resources as optimized by Turbonomic provider

To identify resources as optimized by the Turbonomic provider, it is recommended to add a Turbonomic-specific tag to the resources, as shown below.

#### Option 1 (Recommended)

```hcl
tags = provider::turbonomic::get_tag()  //merge function can be used for adding multiple tags
```

In case of GCP, labels need to be used as shown below

```hcl
labels = provider::turbonomic::get_tag()  //merge function can be used for adding multiple tags
```

#### Option 2

```hcl
tags = {
    turbonomic_optimized_by = "turbonomic-terraform-provider"
  }
```

In case of GCP, labels need to be used as shown below

```hcl
  labels = {
    turbonomic_optimized_by = "turbonomic-terraform-provider"
  }
```

### AWS EC2 example

The `instance_type` is configured to use the turbonomic_cloud_entity_recommendation data source unless
null is returned.  If null is returned, then the default size defined in data block of "t3.nano" is used.

```terraform
provider "aws" {
  region = "us-east-1"
}

data "turbonomic_cloud_entity_recommendation" "example" {
  entity_name  = "exampleVirtualMachine"
  entity_type  = "VirtualMachine"
  default_size = "t3.nano"
}

resource "aws_instance" "terraform-demo-ec2" {
  ami           = "ami-079db87dc4c10ac91"
  instance_type = data.turbonomic_cloud_entity_recommendation.example.new_instance_type

  tags = merge(
    {
      Name = "exampleVirtualMachine"
    },
    provider::turbonomic::get_tag() //tag the resource as optimized by Turbonomic provider
  )
}
```

### Azure example

The `size` is configured to use the `turbonomic_cloud_entity_recommendation` data source unless null is
returned, in which case it uses `Standard_B1s` by default.

```terraform
provider "azurerm" {
  features {}
}

data "turbonomic_cloud_entity_recommendation" "example" {
  entity_name  = "exampleVirtualMachine"
  entity_type  = "VirtualMachine"
  default_size = "Standard_B1s"
}

resource "azurerm_linux_virtual_machine" "exampleVirtualMachine" {
  name                  = "exampleVirtualMachine"
  resource_group_name   = azurerm_resource_group.rg.name
  location              = azurerm_resource_group.rg.location
  size                  = data.turbonomic_cloud_entity_recommendation.example.new_instance_type
  admin_username        = "azureuser"
  network_interface_ids = [azurerm_network_interface.nic.id]
  tags                  = provider::turbonomic::get_tag() //tag the resource as optimized by Turbonomic provider

  admin_ssh_key {
    username   = "azureuser"
    public_key = file("~/.ssh/id_rsa.pub")
  }

  os_disk {
    caching              = "ReadWrite"
    storage_account_type = "Standard_LRS"
  }

  source_image_reference {
    publisher = "Canonical"
    offer     = "UbuntuServer"
    sku       = "18.04-LTS"
    version   = "latest"
  }
}
```

### GCP example

The `machine_type` is set to use the `turbonomic_cloud_entity_recommendation` data source unless null is returned, in which case it uses `e2-micro` by default.

```terraform
provider "google" {
  project = "example-turbonomic-terraform"
  region  = "us-central1"
}

data "turbonomic_cloud_entity_recommendation" "example" {
  entity_name  = "exampleVirtualMachine"
  entity_type  = "VirtualMachine"
  default_size = "n1-standard-1"
}

resource "google_compute_instance" "exampleVirtualMachine" {
  name         = "exampleVirtualMachine"
  machine_type = data.turbonomic_cloud_entity_recommendation.example.new_instance_type
  zone         = "us-central1-a"
  labels       = provider::turbonomic::get_tag() //tag the resource as optimized by Turbonomic provider

  boot_disk {
    initialize_params {
      image = "debian-cloud/debian-11"
    }
  }

  network_interface {
    network = "default"
    access_config {}
  }
}
```

### AWS RDS example

The AWS RDS resource is confgured to use the `turbonomic_aws_db_instance` data source unless null is returned, in which case it defaults to `db.t3.small` for the
`default_instance_class` and `gp2` for the `default_storage_type`.

```terraform
provider "aws" {
  region = "us-east-1"
}

data "turbonomic_aws_db_instance" "rdsExample" {
  entity_name            = "exampleDBinstance"
  default_instance_class = "db.t3.small"
  default_storage_type   = "gp2"
}


resource "aws_db_instance" "default" {
  identifier           = "exampleDBinstance"
  allocated_storage    = 10
  db_name              = "mydb"
  engine               = "mysql"
  engine_version       = "8.0"
  instance_class       = data.turbonomic_aws_db_instance.rdsExample.default_instance_class
  storage_type         = data.turbonomic_aws_db_instance.rdsExample.default_storage_type
  username             = "dbuser"
  password             = "dbpassword"
  parameter_group_name = "default.mysql8.0"
  skip_final_snapshot  = true
  tags                 = provider::turbonomic::get_tag() //tag the resource as optimized by Turbonomic provider
}
```

### AWS EBS example

The AWS EBS resource is confgured to use the `turbonomic_aws_ebs_volume` data source unless null is returned, in which case it defaults to `gp2` for the `default_type`.

```terraform
provider "aws" {
  region = "us-east-1"
}

data "turbonomic_aws_ebs_volume" "example" {
  entity_name  = "exampleEBSVolumeName"
  default_type = "gp2"
}

resource "aws_ebs_volume" "example-ebs-instance" {
  availability_zone = "us-east-1a"
  size              = 100
  type              = data.turbonomic_aws_ebs_volume.example.new_type
  iops              = 3000
  throughput        = 125
  tags = merge(
    {
      Name = "exampleEBSVolumeName"
    },
    provider::turbonomic::get_tag() //tag the resource as optimized by Turbonomic provider
  )
}
```

### Azure Managed Disks example

The `storage_account_type` is set to use the `turbonomic_azurerm_managed_disk` data source unless null is returned, in which case it uses `Standard_LRS` by default.

```terraform
data "turbonomic_azurerm_managed_disk" "example" {
  entity_name                  = "example-azuredata-disk"
  default_storage_account_type = "Standard_LRS"
}

resource "azurerm_managed_disk" "example_disk" {
  name                 = "example-azuredata-disk"
  location             = "East US"
  resource_group_name  = "AppInfra_Integrations"
  storage_account_type = data.turbonomic_azurerm_managed_disk.example.new_storage_account_type
  create_option        = "Empty"
  disk_size_gb         = 32
  tags                 = provider::turbonomic::get_tag() //tag the resource as optimized by Turbonomic provider
}
```

### Google Persistent Disk example

The Google Persistent Disk resource is confgured to use the `turbonomic_google_compute_disk` data source unless null is returned,
in which case it uses `pd-standard` by default.

```terraform
provider "google" {
  project = "example-turbonomic-terraform"
  region  = "us-central1"
}

data "turbonomic_google_compute_disk" "example" {
  entity_name  = "example-gcp-data-disk"
  default_type = "pd-standard"
}

resource "google_compute_disk" "default" {
  name  = "example-gcp-data-disk"
  type  = data.turbonomic_google_compute_disk.example.default_type
  zone  = "us-central1-a"
  image = "debian-11-bullseye-v20220719"
  labels = merge(
    {
      environment = "dev"
    },
    provider::turbonomic::get_tag() //tag the resource as optimized by Turbonomic provider
  )
  physical_block_size_bytes = 4096
}
```
