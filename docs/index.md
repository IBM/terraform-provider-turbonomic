---
layout: ""
page_title: "Provider: " IBM Turbonomic
description: |-
  The Turbonomic provider provides data sources to interact with the Turbonomic API.
---

# IBM Turbonomic Provider

The Turbonomic provider is used to interact with resources within Turbonomic. The provider
needs to be configured with the hostname and proper credentials before it can be used.

The provider enables you to use Turbonomic as the source of truth for scaling decisions for
your cloud entities deployed through Terraform.  The data source of the provider will return
the tier size based on the recommendation Turbonomic is making, or the current size if no
scaling actions exist.  It is also recommended to include a default for cases where the
entity has not yet been deployed.

## Example Usage

### Configure the Provider

```terraform
terraform {
  required_providers {
    turbonomic = {
      source  = "github.ibm.com/turbonomic/terraform-provider-turbonomic"
      version = "1.0.0"
    }
  }
}

provider "turbonomic" {
  username   = var.username
  password   = var.password
  hostname   = var.hostname
  skipverify = var.skipverify
}
```

### Configuring Data Sources

```terraform
data "turbonomic_cloud_entity_recommendation" "example" {
  entity_name = "exampleVirtualMachine"
  entity_type = "VirtualMachine"
}
```

### AWS Example

The _instance_type_ is configured to use the turbonomic_cloud_entity_recommendation data source unless
null is returned.  If this happens, a default of "t3.nano" is used.

```terraform
provider "aws" {
  region = "us-east-1"
}

data "turbonomic_cloud_entity_recommendation" "example" {
  entity_name = "exampleVirtualMachine"
  entity_type = "VirtualMachine"
}

resource "aws_instance" "terraform-demo-ec2" {
  ami = "ami-079db87dc4c10ac91"
  instance_type = (
    data.turbonomic_cloud_entity_recommendation.example.new_instance_type != null
    ? data.turbonomic_cloud_entity_recommendation.example.new_instance_type
    : "t3.nano"
  )
  tags = {
    Name = "terraform-demo-ec2"
  }
}
```

### Azure Example

The _size_ is configured to use the _turbonomic_cloud_entity_recommendation_ data source unless null is
returned.  If this happens, a default of "Standard_B1s" is used.

```terraform
provider "azurerm" {
  features {}
}

data "turbonomic_cloud_entity_recommendation" "example" {
  entity_name = "exampleVirtualMachine"
  entity_type = "VirtualMachine"
}

resource "azurerm_linux_virtual_machine" "exampleVirtualMachine" {
  name                = "exampleVirtualMachine"
  resource_group_name = azurerm_resource_group.rg.name
  location            = azurerm_resource_group.rg.location
  size = (
    data.turbonomic_cloud_entity_recommendation.example.new_instance_type != null
    ? data.turbonomic_cloud_entity_recommendation.example.new_instance_type
    : "Standard_B1s"
  )
  admin_username        = "azureuser"
  network_interface_ids = [azurerm_network_interface.nic.id]

  admin_ssh_key {
    username   = "azureuser"
    public_key = file("~/.ssh/id_rsa.pub")
  }

  os_disk {
    caching              = "ReadWrite"
    storage_account_type = "Standard_LRS"
  }

  source_image_reference {
    publisher = "Canonical"
    offer     = "UbuntuServer"
    sku       = "18.04-LTS"
    version   = "latest"
  }
}
```

### GCP Example

The _machine_type_ is set to use the _turbonomic_cloud_entity_recommendation_ data source unless null is
returned.  If this happens, a default of "e2-micro" is used.

```terraform
provider "google" {
  project = "example-turbonomic-terraform"
  region  = "us-central1"
}

resource "google_compute_instance" "exampleVirtualMachine" {
  name = "exampleVirtualMachine"
  machine_type = (
    data.turbonomic_cloud_entity_recommendation.example.new_instance_type != null
    ? data.turbonomic_cloud_entity_recommendation.example.new_instance_type
    : "e2-micro"
  )
  zone = "us-central1-a"

  boot_disk {
    initialize_params {
      image = "debian-cloud/debian-11"
    }
  }

  network_interface {
    network = "default"
    access_config {}
  }
}
```

<!-- schema generated by tfplugindocs -->
## Schema

### Optional

- `hostname` (String) Hostname or IP Address of Turbonomic Instance
- `password` (String, Sensitive) Password for the username to access the Turbonomic Instance
- `skipverify` (Boolean) Boolean on whether to verify the SSL/TLS certificate for the hostname
- `username` (String) Username to access the Turbonomic Instance
